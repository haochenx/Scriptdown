{
  var Invocation = function(id,payload,arg) {
    this.id = id;
    this.payload = payload;
    if (arg) this.arg = arg;
  }

  var Macro = function(id,payload,arg) {
    Invocation.call(this,id,payload,arg);
  }
  Macro.prototype = Object.create(Invocation.prototype);

  var ExternalRenderer = function(id,payload,arg) {
    Invocation.call(this,id,payload,arg);
  }
  ExternalRenderer.prototype = Object.create(Invocation.prototype);

  var CrossRef = function(id,payload) {
    Invocation.call(this,id,payload);
  }
  CrossRef.prototype = Object.create(Invocation.prototype);

  var CompilerInstruction = function(id,payload) {
    this.id = id;
    this.payload = payload;
  }

  var chainInvocations = function(payload,invocationChain) {
    return {payload: payload, chain: invocationChain }
  }
}

start
  = pieces

piece
  = bracket_compinst
  / bracket_invocation
  / tag_invocation
  / tag_compinst
  / text

pieces
  = first:piece rest:pieces { return [first].concat(rest); }
  / first:piece { return [first]; }
  /

alphabet = [A-Za-z]
alphanum = alphabet / [0-9]
punct = [!-//:-@\[-`{-~]
whitespace = [ \n\r\t]
nonbr_whitespace = [ \t]

escape = "\\"
escaped = [\\\[\]<]
unescaped
  = alphanum / " " / (! escaped) ch:punct { return ch; }

char
  = unescaped
  / escape seq:(
             "[" { return "["; } /
             "]" { return "]"; } /
             "<" { return "<"; } /
             "n" { return "\n"; } /
             "r" { return "\r"; } /
             "t" { return "\t"; }
           ) { return seq; }
punct_char
  = (! alphanum) char:char { return char; }

whitespaces = whitespace+ { return " "; }
nonbr_whitespaces = nonbr_whitespace+ { return " "; }

word = chars:alphanum+ { return chars.join(""); }
puncts = puncts:punct_char+ { return puncts.join(""); }
text_token
  = word / puncts / whitespaces
text
  = first:text_token rest:text { return [first].concat(rest); }
  / first:text_token { return [first]; }

id_first_char = alphabet / [._]
id_rest_char = id_first_char / alphanum

id = first:id_first_char rest:id_rest_char* { return [first].concat(rest).join(""); }

macro_id = id
exrend_id = "!" id:id { return id; }
crossref_id = "@" id:id { return id; }
compinst_id = "!!" id:id { return id; }

bracket_invocation
  = payload:bracket_payload_parsed "(" first:bracket_invocation_list_parsed rest:bracket_invocation_list_rest ")"
    { return chainInvocations(payload, [first].concat(rest)); }
  / payload:bracket_payload_unparsed "(" first:bracket_invocation_list_unparsed rest:bracket_invocation_list_rest ")"
    { return chainInvocations(payload, [first].concat(rest)); }

bracket_payload_parsed
  = "[" payload:pieces "]" { return payload; }

bracket_payload_unparsed
  = "[" payload:char+ "]" { return payload.join(""); }

bracket_invocation_list_parsed
  = id:macro_id arg:bracket_invocation_arg? { return { type: "Macro", id: id, arg: arg }; }
  / id:crossref_id { return {type: "CrossRef", id: id}; }

bracket_invocation_list_unparsed
  = id:exrend_id arg:bracket_invocation_arg? { return { type: "ExternalRenderer", id: id, arg: arg }; }

bracket_invocation_list_rest
  = ";" first:bracket_invocation_list_rest_comp rest:bracket_invocation_list_rest { return [first].concat(rest); }
  / ";" first:bracket_invocation_list_rest_comp { return [first]; }
  /

bracket_invocation_list_rest_comp
  = id:macro_id arg:bracket_invocation_arg? { return { type: "Macro", id: id, arg: arg }; }
  / id:exrend_id arg:bracket_invocation_arg? { return { type: "ExternalRenderer", id: id, arg: arg }; }
  / id:crossref_id { return {type: "CrossRef", id: id}; }

bracket_invocation_arg
  = ":" arg:word { return arg; }

bracket_compinst
  = payload:bracket_payload_unparsed "(" id:compinst_id ")"
    { return new CompilerInstruction(id,payload); }

tag_invocation
  = "<" otag:tag_invocation_tag_parsed ">" payload:tag_payload_parsed "<" "/" ctag:tag_invocation_close_tag ">"
    & { return ctag.type == otag.type && ctag.id == otag.id }
    { return otag.constructor(payload); }
  / "<" otag:tag_invocation_tag_unparsed ">" payload:tag_payload_unparsed "<" "/" ctag:tag_invocation_close_tag ">"
    & { return ctag.type == otag.type && ctag.id == otag.id }
    { return otag.constructor(payload); }

tag_payload_parsed
  = payload:pieces { return payload; }

tag_payload_unparsed
  = payload:(char / whitespace)* { return payload.join(""); }

tag_invocation_tag_parsed
  = id:macro_id arg:tag_invocation_arg?
    { return { type: "Macro", id: id, arg: arg,
               constructor: function (payload) { return new Macro(id,payload,arg)} }; }
  / id:crossref_id 
    { return {type: "CrossRef", id: id,
               constructor: function (payload) { return new CrossRef(id,payload)} }; }

tag_invocation_tag_unparsed
  = id:exrend_id arg:tag_invocation_arg?
    { return { type: "ExternalRenderer", id: id, arg: arg,
               constructor: function (payload) { return new ExternalRenderer(id,payload,arg)} }; }

tag_invocation_close_tag
  = id:macro_id { return { type: "Macro", id: id }; }
  / id:crossref_id { return {type: "CrossRef", id: id}; }
  / id:exrend_id { return { type: "ExternalRenderer", id: id }; }

tag_invocation_arg
  = nonbr_whitespaces arg:word { return arg; }

tag_compinst
  = "<" oid:compinst_id ">" payload:tag_payload_unparsed "<" "/" cid:compinst_id ">"
    & { return oid == cid; }
    { return new CompilerInstruction(oid,payload); }
